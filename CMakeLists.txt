# cmake版本
CMAKE_MINIMUM_REQUIRED(VERSION 3.7.2)

# 项目名
PROJECT(libcommon VERSION 1.0)

# 开关
OPTION(VLD "use Visual Leak Detector to check memory on windows" ON)
OPTION(RELEASE "compile the release version" OFF)

# 设置模块路径
SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/3rd/vld)

# 平台
IF(MSVC)
	SET(WINDOWS ON)
	SET(LINUX OFF)
ELSEIF(CMAKE_COMPILER_IS_GNUCXX)
	SET(WINDOWS OFF)
	SET(LINUX ON)
	SET(VLD OFF)
ENDIF()

# 显示参数
MESSAGE(STATUS "VLD:" ${VLD})
MESSAGE(STATUS "RELEASE:" ${RELEASE})
MESSAGE(STATUS "WINDOWS:" ${WINDOWS})
MESSAGE(STATUS "LINUX:" ${LINUX})

# 添加源文件
SET(common_srcs
	${PROJECT_SOURCE_DIR}/src/Allocator.cc
	${PROJECT_SOURCE_DIR}/src/CExpection.cc
	${PROJECT_SOURCE_DIR}/src/CObject.cc
	${PROJECT_SOURCE_DIR}/src/LocalTime.cc
	${PROJECT_SOURCE_DIR}/src/SDString.cc
	${PROJECT_SOURCE_DIR}/src/Thread.cc
	${PROJECT_SOURCE_DIR}/src/TimeStamp.cc
)

# 设置输出目录
SET(CMAKE_DEBUG_POSTFIX "d")
SET(CMAKE_SUPPRESS_REGENERATION true)
SET(OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/output)
SET(OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY})
SET(OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY})
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY_DEBUG})
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY_DEBUG})
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY_DEBUG})
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY_RELEASE})
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY_RELEASE})
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY_RELEASE})

# 编译项
IF(WINDOWS)
	SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /MDd")
	SET(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /MD")
	ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS)
	ADD_DEFINITIONS(-D_CRT_NONSTDC_NO_WARNINGS)
	ADD_COMPILE_OPTIONS(/utf-8)
ELSEIF(LINUX)
	SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0 -Wall -ggdb")
	SET(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -O2 -Wall")
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pthread -std=c++11")
	ADD_DEFINITIONS(-m64)
ENDIF()

# 显式输出编译选项
IF(RELEASE)
	SET(CMAKE_BUILD_TYPE Release)
	MESSAGE(STATUS "flags:" ${CMAKE_CXX_FLAGS_RELEASE} ${CMAKE_CXX_FLAGS})
ELSE()
	SET(CMAKE_BUILD_TYPE Debug)
	MESSAGE(STATUS "flags:" ${CMAKE_CXX_FLAGS_DEBUG} ${CMAKE_CXX_FLAGS})
ENDIF()

# googletest
SET(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
ADD_SUBDIRECTORY(3rd/googletest)
SET(GTEST_GTEST_LIBRARY optimized gtest debug gtestd)
SET(GTEST_GMOCK_LIBRARY optimized gmock debug gmockd)
SET(GTEST_LIBRARIES ${GTEST_GTEST_LIBRARY} ${GTEST_GMOCK_LIBRARY} CACHE DOC "gtest all library" FORCE)

# 头文件
INCLUDE_DIRECTORIES(${PROJECT_SOURCE_DIR}/include)
INCLUDE_DIRECTORIES(${PROJECT_SOURCE_DIR}/3rd/googletest/googletest/include)
INCLUDE_DIRECTORIES(${PROJECT_SOURCE_DIR}/3rd/googletest/googlemock/include)

# 库目录
LINK_DIRECTORIES(${OUTPUT_DIRECTORY})

# vld
IF(VLD)
	FIND_PACKAGE(VLD)
	IF(VLD_FOUND)
		ADD_DEFINITIONS(-DUSE_VLD)
		INCLUDE_DIRECTORIES(${VLD_INCLUDE_DIRS})
	ENDIF()
ENDIF()

# 生成静态库、动态库
ADD_LIBRARY(common-static STATIC ${common_srcs})
ADD_LIBRARY(common SHARED ${common_srcs})
IF(WINDOWS)
	TARGET_LINK_LIBRARIES(common ws2_32 iphlpapi psapi userenv)
	TARGET_COMPILE_DEFINITIONS(common PRIVATE BUILDING_COMMON_SHARED)
ELSEIF(LINUX)
	TARGET_COMPILE_OPTIONS(common PRIVATE -fPIC -fvisibility=hidden)
ENDIF()

# 添加单元测试项目
ADD_SUBDIRECTORY(test)